import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/' }
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.4'
        classpath "org.akhikhl.gretty:gretty:2.0.0"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "eclipse"

    // required for the GWT stuff to work
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    ext {
        gdxVersion = "1.7.1"
        tweenVersion = "6.3.3"
    }

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    tasks.eclipse.doLast {
        delete ".project"
    }

    jar {
        manifest {
            attributes(
                    "Specification-Title": "${project.name}",
                    "Specification-Version": "${project.version}",
                    "Specification-Vendor": "flixel-gdx team (https://github.com/flixel-gdx)",
                    "Implementation-Vendor": "org.flixel",
                    "Built-Date": new SimpleDateFormat("yyyy-MM-dd HH:mm:ss Z").format(new Date()),
            )
        }
    }
}